[Setup]
AppName=Abstract Script Engines
AppVerName=Abstract Script Engines 1.0.3893
AppPublisher=Gentype
AppPublisherURL=http://www.gentype.com/
AppSupportURL=http://www.gentype.com/
AppUpdatesURL=http://www.gentype.com/
DefaultDirName={pf}\ASE
DefaultGroupName=Abstract Script Engines
ChangesEnvironment=yes
OutputDir=.
OutputBaseFilename=asesetup
Compression=lzma
SolidCompression=yes
PrivilegesRequired=admin
MinVersion=5.0,5.0
; WizardImageFile=compiler:WizardImages\Modern\InternetModern14.bmp
WizardImageStretch=no
WizardImageBackColor=$420200
; WizardSmallImageFile=compiler:WizardImages\Modern\Small\InternetModernSmall14.bmp

[Languages]
; Name: "japanese"; MessagesFile: "compiler:Languages\japanese.isl"

; [Tasks]
; Name: "desktopicon"; Description: "{cm:CreateDesktopIcon}"; GroupDescription: "{cm:AdditionalIcons}"; Flags: unchecked

[Files]
Source: "..\aserun.exe"; DestDir: "{app}"; Flags: ignoreversion restartreplace
Source: "..\aserunw.exe"; DestDir: "{app}"; Flags: ignoreversion restartreplace
Source: "..\libase.lib"; DestDir: "{app}"; Flags: ignoreversion restartreplace
Source: "..\libase.dll"; DestDir: "{app}"; Flags: ignoreversion restartreplace
Source: "..\libase-scr-CLR.dll"; DestDir: "{app}"; Flags: ignoreversion restartreplace
Source: "..\libase-scr-COM.dll"; DestDir: "{app}"; Flags: ignoreversion restartreplace
Source: "..\libase-scr-Dummy.dll"; DestDir: "{app}"; Flags: ignoreversion restartreplace
Source: "..\libase-scr-JS.dll"; DestDir: "{app}"; Flags: ignoreversion restartreplace
Source: "..\libase-scr-Java.dll"; DestDir: "{app}"; Flags: ignoreversion restartreplace
Source: "..\libase-scr-Lua.dll"; DestDir: "{app}"; Flags: ignoreversion restartreplace
Source: "..\libase-scr-CPP.dll"; DestDir: "{app}"; Flags: ignoreversion restartreplace
Source: "..\asejni.jar"; DestDir: "{app}"; Flags: ignoreversion restartreplace
Source: "..\prebuiltjar\*.jar"; DestDir: "{app}"; Flags: ignoreversion restartreplace
Source: "..\aseconf_d\*.aseconf"; DestDir: "{app}\aseconf_d"; Flags: ignoreversion restartreplace
Source: "..\ase\*.hpp"; DestDir: "{app}\ase"; Flags: ignoreversion restartreplace
Source: "..\samples\*.*"; DestDir: "{app}\samples"; Flags: ignoreversion restartreplace
Source: "..\svrsamples\*"; Excludes: ".svn"; DestDir: "{app}\svrsamples"; Flags: recursesubdirs ignoreversion restartreplace
Source: "..\README.html"; DestDir: "{app}"; Flags: recursesubdirs ignoreversion restartreplace
Source: "..\..\pmcjs\libpmcjs.dll"; DestDir: "{app}"; Flags: ignoreversion restartreplace
Source: "..\..\depslns\js\Release\js32.dll"; DestDir: "{app}"; Flags: ignoreversion restartreplace
Source: "..\..\deps\nsprpub\dist\lib\libnspr4.dll"; DestDir: "{app}"; Flags: ignoreversion restartreplace
Source: "..\..\deps\nsprpub\pr\src\libnspr4.dll.manifest"; DestDir: "{app}"; Flags: ignoreversion restartreplace
Source: "C:\Program Files\Microsoft Visual Studio 9.0\VC\redist\x86\Microsoft.VC90.CRT\msvcr90.dll"; DestDir: "{app}"; Flags: ignoreversion restartreplace
Source: "C:\Program Files\Microsoft Visual Studio 9.0\VC\redist\x86\Microsoft.VC90.CRT\msvcp90.dll"; DestDir: "{app}"; Flags: ignoreversion restartreplace
Source: "C:\Program Files\Microsoft Visual Studio 9.0\VC\redist\x86\Microsoft.VC90.CRT\msvcm90.dll"; DestDir: "{app}"; Flags: ignoreversion restartreplace
Source: "C:\Program Files\Microsoft Visual Studio 9.0\VC\redist\x86\Microsoft.VC90.CRT\Microsoft.VC90.CRT.manifest"; DestDir: "{app}"; Flags: ignoreversion restartreplace
Source: "C:\Program Files\Microsoft Visual Studio 9.0\VC\redist\x86\Microsoft.VC90.ATL\atl90.dll"; DestDir: "{app}"; Flags: ignoreversion restartreplace
Source: "C:\Program Files\Microsoft Visual Studio 9.0\VC\redist\x86\Microsoft.VC90.ATL\Microsoft.VC90.ATL.manifest"; DestDir: "{app}"; Flags: ignoreversion restartreplace
Source: "C:\WINDOWS\system32\msvcr71.dll"; DestDir: "{app}"; Flags: ignoreversion restartreplace
  ; FIXME

[Icons]
Name: "{group}\Command Shell"; Filename: "cmd.exe"; WorkingDir: "{app}"
Name: "{group}\{cm:UninstallProgram,ASE Runtime}"; Filename: "{uninstallexe}"

[Tasks]
Name: setenvvar; Description: "Add ASE to the PATH &environment variable"

; [Run]
; Filename: "{app}\README.html"; Description: "View the README file"; Flags: postinstall shellexec skipifsilent

[Code]
procedure AddPath();
var
  appdir, pathstr, pstr, ent : String;
  idx : Integer;
  found : Boolean;
begin
  appdir := ExpandConstant('{app}');
  if (RegQueryStringValue(HKEY_LOCAL_MACHINE,
    'SYSTEM\CurrentControlSet\Control\Session Manager\Environment', 'PATH', pathstr)) then begin
    pstr := pathstr;
    while Length(pstr) > 0 do begin
      idx := Pos(';', pstr);
      if (idx > 0) then begin
        ent := Copy(pstr, 0, idx - 1);
        Delete(pstr, 1, idx);
      end else begin
        ent := pstr;
        pstr := '';
      end;
      if (appdir = ent) then begin
        found := True;
      end;
    end;
    if not found then begin
      pathstr := pathstr + ';' + appdir;
      RegWriteExpandStringValue(HKEY_LOCAL_MACHINE,
        'SYSTEM\CurrentControlSet\Control\Session Manager\Environment', 'PATH', pathstr);
    end;
  end;
  RegWriteStringValue(HKEY_LOCAL_MACHINE,
    'SYSTEM\CurrentControlSet\Control\Session Manager\Environment', 'ASE_LIBEXEC', appdir);
end;

procedure RemovePath();
var
  appdir, pathstr, newpathstr, pstr, ent : String;
  idx : Integer;
  found : Boolean;
begin
  appdir := ExpandConstant('{app}');
  if (RegQueryStringValue(HKEY_LOCAL_MACHINE,
    'SYSTEM\CurrentControlSet\Control\Session Manager\Environment', 'PATH', pathstr)) then begin
    pstr := pathstr;
    while Length(pstr) > 0 do begin
      idx := Pos(';', pstr);
      if (idx > 0) then begin
        ent := Copy(pstr, 0, idx - 1);
        Delete(pstr, 1, idx);
      end else begin
        ent := pstr;
        pstr := '';
      end;
      if (appdir = ent) then begin
        found := True;
      end else begin
        if (Length(newpathstr) > 0) then begin
          newpathstr := newpathstr + ';';
        end;
        newpathstr := newpathstr + ent;
      end;
    end;
    if found then begin
      RegWriteStringValue(HKEY_LOCAL_MACHINE,
        'SYSTEM\CurrentControlSet\Control\Session Manager\Environment', 'PATH', newpathstr);
    end;
  end;
  RegDeleteValue(HKEY_LOCAL_MACHINE,
    'SYSTEM\CurrentControlSet\Control\Session Manager\Environment', 'ASE_LIBEXEC');
end;

procedure CurStepChanged(CurStep: TSetupStep);
begin
  case CurStep of
    ssPostInstall:
      AddPath();
  end;
end;

procedure CurUninstallStepChanged(CurUninstallStep: TUninstallStep);
begin
  case CurUninstallStep of
    usPostUninstall:
      RemovePath();
  end;
end;
